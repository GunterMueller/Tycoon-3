class X_TL2TypeChecker
super X_TL2TypeCheckMixin, X_TL2ClassManagerObserver, X_TL2PoolObserver, X_TL2
(*
 * This file is part of the Tycoon-2 system.
 *
 * The Tycoon-2 system is free software; you can redistribute it and/or
 * modify it under the terms of the GNU General Public License
 * as published by the Free Software Foundation (Version 2).
 *
 * The Tycoon-2 system is distributed in the hope that it will be useful,
 * but WITHOUT ANY WARRANTY; without even the implied warranty
 * of MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.
 * See the GNU General Public License for more details.
 * 
 * You should have received a copy of the GNU General Public
 * License along with the Tycoon-2 system; see the file LICENSE.
 * If not, write to AB 4.02, Softwaresysteme, TU Hamburg-Harburg
 * D-21071 Hamburg, Germany. (http://www.sts.tu-harburg.de)
 * 
 * Copyright (c) 1996-1998 Higher-Order GmbH, Hamburg. All rights reserved.
 *
 database of proven/being proved subtype relations *)
meta X_TL2TypeCheckerClass
{
logTypeChecks   :Bool

  (* dump checkType *)

logValueChecks  :Bool

  (* dump checkValue *)

logSubTypeTests :Bool

  (* dump checkSubType *)

abortCheck      :Bool

  (* abort now ! *)

maxDepth        :Int
  (* recursion limitation *)

checkClass(c :X_TL2ClassPublic) :Void
  (* public interface to typechecking *)
{
    protect({
      trace(c.pos, "While checking class "+c.name, {
	_currentClass := c
	_depth := 0
	unificationDB.reset

	tycoon.stdout.writeln("Checking class "+c.name)

	c.domain.do(fun(i :X_TL2Ide) {
	  trace(i.pos, "While checking formal type parameter "+i.name, {
	    i.type.checkType(X_TL2EmptySubst.instance)
	  })
	})

	checkMeta(c)
	checkSelf(c)
	checkCPL(c)

	c.methodDictionary.do(fun(m :X_TL2Method) { checkMethod(m) })
      })
      agenda.removeKey(c.name)
    }, {
      unificationDB.reset
      _currentClass := nil
    })
  }

check :Void
  (* *)
{
  tycoon.stdout << "Computing checking order for "<<agenda.size<<" classes...\n"
  plan ::= agenda.as(MutableArray)
  rating ::= fun(c :X_TL2ClassPublic) :Int {
    dependencies.lookup(c.name, fun(dep :Set(String)) { dep.size }, { 0 })
  }
  plan.quicksort(fun(a :X_TL2ClassPublic, b :X_TL2ClassPublic) {
    ;; higher rating comes earlier
    rating[b] - rating[a]
  })

  c :X_TL2ClassPublic := nil
  try({
    plan.do(fun(cl :X_TL2ClassPublic) {
      c := cl  ;; for exception handler
      checkClass(cl)
    })
  } else: fun(e :Exception) {
    unificationDB.reset
    tycoon.stdout << e << "\n"
    tycoon.stdout << c.name << " has not been successfully typechecked.\n"
    nil
  })
}

checkValue(value :X_TL2Value) :X_TL2Type
  (* *)
{
    _currentClass := nil
    unificationDB.reset
    _depth := 0

    protect({
      value.check(fun(t :X_TL2Type, s :X_TL2Subst) { t.reduce(s, fun(t :X_TL2Type, :X_TL2Subst) { t }) })
    }, {
      unificationDB.reset
    })
  }

ignore(c :X_TL2ClassPublic) :Void
  (* *)
{
    agenda.removeKey(c.name)
  }

currentClass :X_TL2ClassPublic
  (* *)
{ _currentClass }

classChanged(:X_TL2ClassManager, c :X_TL2ClassPublic) :Void
  (* observing X_TL2ClassManager::set *)
{
    ; give a type to c''s typeIde 
    c.typeIde.type := if( c.domain.isEmpty then: fun() :X_TL2Type {
      t :X_TL2Type := X_TL2ClassInstanceType.new(c)
    } else: {
      X_TL2OperType.new(c.domain, X_TL2ClassInstanceType.new(c))
    })

    agenda[c.name] := c

    varianceDB.removeKey(c.typeIde)

    ; ## too slow ? 
    dependencies.do(fun(s :Set(String)) {
      s.remove(c.name)
    })

    ; globalIdeChanged is called when this class is inserted into the pool 
  }

globalIdeChanged(i :String) :Void
  (* observing the pool *)
{
    if( dependencies.includesKey(i) then: {
      dependencies[i].do(fun(c :String) {
        dependingClass ::= classManager.get(c)
        agenda[c] := dependingClass
      })
    })

    if( classManager.classTable.includesKey(i) then: {
      ; is i a class ??? 
      classManager.subclasses(classManager.get(i)).do(fun(c :X_TL2ClassPublic) {
	; classes inheriting from the changed class
;	   will have to adjust their cplSubst since it
;	   contains the superclasses'' formal type params
;	   which change if the class is recompiled
;	

	t ::= c.asType
	if( t.isNotNil then: {
	  t.visitor.
	    ifClassInstanceType(fun(cit :X_TL2ClassInstanceType) { cit.resetCPL }).
	    ifOperType(fun(ot :X_TL2OperType) {
	      ot.visitor.ifClassInstanceType(fun(cit :X_TL2ClassInstanceType) { cit.resetCPL }).visitType(ot.range)
	    }).
	  visitType(t)
	})
      })
    })
  }

globalIdeReferenced(i :String) :Void
  (* called in method-lookup / typeIde resolution *)
{
    if( currentClass.isNotNil then: {
      dependencies.lookup(i, fun(dependants :Set(String)) {
	dependants.add(currentClass.name)
      }, {
	dependencies[i] := Set.with1(currentClass.name)
      })
    })
  }

typeChecked(type :X_TL2Type, subst :X_TL2Subst, check :Fun():Void) :Void
  (* called by types *)
{
    abort
  
    if( logTypeChecks then: {
      tycoon.stdout << "Checking type :" << type << subst << "\n"
    })
    
    check[]
  }

valueChecked(T <: Void, v :X_TL2Value, check :Fun():T) :T
  (* *)
{
    abort
  
    if( logValueChecks then: {
      tycoon.stdout << "Checking value :" << v << "\n"
    })
    
    check[]
  }

checkSubType(small :X_TL2Type, big :X_TL2Type, sSubst :X_TL2Subst, bSubst :X_TL2Subst, check :Fun():Void) :Void
  (* *)
{
    abort
  
    if( logSubTypeTests then: {
      tycoon.stdout <<
        "{ Checking subtype :" <<
	small <<
	sSubst <<
	" <: " <<
	big <<
	bSubst <<
	"\n"
    })
    
    subTypeDB.consult(small, big, sSubst, bSubst, {
      unistate ::= unificationState
      try({
        check[]
      } else: fun(e :Exception) {
        retractTo(unistate)
	if( logSubTypeTests then: { tycoon.stdout << "FAILED }\n" })  
	e.raise
      })
    })

    if( logSubTypeTests then: { tycoon.stdout << "DONE }\n" })
  }

saveUnivar(u :X_TL2UnifyType) :Void
  (* *)
{
    unificationDB.saveUnivar(u)
  }

unificationState :Int
  (* *)
{
    unificationDB.unificationState
  }

retractTo(unificationState :Int) :Void
  (* *)
{
    unificationDB.retractTo(unificationState)
  }

finalize :Void
  (* regain some MB *)
{
  subTypeDB := X_TL2SubTypeDB.new
  varianceDB := Dictionary.new
  unificationDB.reset
}

classManager :X_TL2ClassManager
  (* *)
{ _classManager }

pool :X_TL2Pool
  (* *)
{ _pool }

getVariance(operIde :X_TL2TypeIde) :List(Int)
{
  found ::=
    varianceDB.lookup(operIde, fun(variance:List(Int)) {
      variance
    }, {
      ;; set marker for "in progress" (recursion)
      varianceDB[operIde] := List.with0
      try({
	oper :X_TL2OperType :=
	  operIde.type.accept(
	    X_TL2FunTypeVisitor.new
	    .ifOperType(fun(t:X_TL2OperType){ t })
	    .otherwise(fun(:X_TL2Type){ nil }))
	assert({ oper.isNotNil })

	isSimpleVisitor ::= X_TL2FunTypeVisitor.new
	  .ifIdeType(fun(t:X_TL2IdeType){ t.ide.isGlobal })
	  .ifVoidType(fun(:X_TL2VoidType){ true })
	  .otherwise(fun(:X_TL2Type){ false })
	domain ::= oper.domain
	if( domain.all(fun(param :X_TL2TypeIde) {
	      param.case( fun(:X_TL2ValueIde){ false },
			  fun(:X_TL2TypeIde){ false },
			  fun(st:X_TL2SubTypeIde){ st.type.accept(isSimpleVisitor) })
	    }) then: {
	  emptySubst ::= X_TL2EmptySubst.instance
	  unistate ::= typeChecker.unificationState
	  tycoon.stdout << "{computing variance of operator "<<operIde.name<<"\n"
	  myApply ::= 
	    X_TL2ApplyType.new(X_TL2IdeType.new(operIde),
	      domain.map(fun(ide:X_TL2TypeIde){ X_TL2IdeType.new(ide) }).as(List))
	  variance ::=
	    domain.map(fun(param :X_TL2TypeIde) {
	      subIde ::= X_TL2SubTypeIde.new(param.pos, "sub_"+param.name, X_TL2IdeType.new(param))
	      subIdeSubst ::= X_TL2ConsSubst.new(param, X_TL2IdeType.new(subIde), emptySubst)
	      subApply ::= 
		X_TL2ApplyType.new(X_TL2IdeType.new(operIde),
		  domain.map(fun(ide:X_TL2TypeIde){ 
		    X_TL2IdeType.new(if(ide == param then:{ subIde } else:{ ide })) })
		  .as(List))
	      vari ::= 0 
	      try({
		subTypeDB.consult(subApply, myApply, emptySubst, emptySubst, {
		  oper.range.checkSubType(oper.range,subIdeSubst,emptySubst)
		})
		vari := 1  ;; co
	      } else: fun(e :Exception) {
		if( e."class" != X_TL2TypeCheckTypeError then: {
	          retractTo(unistate)
		  e.raise
		})
	      })
	      retractTo(unistate)
	      try({
		subTypeDB.consult(myApply, subApply, emptySubst, emptySubst, {
		  oper.range.checkSubType(oper.range,emptySubst,subIdeSubst)
		})
		vari := vari + 2 ;; contra
	      } else: fun(e :Exception) {
		if( e."class" != X_TL2TypeCheckTypeError then: {
	          retractTo(unistate)
		  e.raise
		})
	      })
	      retractTo(unistate)
	      tycoon.stdout << "{variance of operator "<<operIde.name<<" in "<<param.name<<": "<<vari<<"}\n"
	      vari
	    }).as(List)
	  tycoon.stdout << "}\n"
	  varianceDB[operIde] := variance
	  variance
	} else: {
	  tycoon.stdout << "{operator signature too complicated for variance check: "<<operIde.name<<"}\n"
	  List.with0
	})
      } else: fun(e:Exception) {
	varianceDB.removeKey(operIde)
	e.raise
      })
    })
  ;; empty list is marker for "too touchy" or "in progress"
  if( !found.isEmpty then: { found } else: { nil })
}

private

subTypeDB     :X_TL2SubTypeDB

varianceDB    :Dictionary(X_TL2TypeIde, List(Int))

unificationDB :X_TL2UnificationDB
  (* database of currently instantiated unification variables *)

_currentClass    :X_TL2ClassPublic
  (* class being currently checked *)

_depth           :Int
  (* # of entries into the typechecker to avoid infinite recursion *)

dependencies    :Dictionary(String, Set(String))
  (* maps from global ides to class names *)

agenda :Dictionary(String, X_TL2ClassPublic)
  (* classes to be typeChecked *)

_classManager :X_TL2ClassManager

_pool :X_TL2Pool

checkMethod(m :X_TL2Method) :Void
  (* *)
{
    m.methodType.positionalDomain.do(fun(i :X_TL2Ide) {
      i.type.checkType(X_TL2EmptySubst.instance)
    })

    m.methodType.keywordDomain.do(fun(kwi :X_TL2KeywordIde) {
      kwi.ide.type.checkType(X_TL2EmptySubst.instance)
      kwi.defaultValue.check(fun(t :X_TL2Type, s :X_TL2Subst) {
	setError(m.pos, "default value does not conform to declared parameter type", {
	  t.checkSubType(kwi.ide.type, s, X_TL2EmptySubst.instance)
	})
      })
    })

    m.methodType.range.checkType(X_TL2EmptySubst.instance)

    ;; ### check pre- and postcondition

    if( m."class" == X_TL2CompiledMethod then: {
      m ::= _typeCast(m, :X_TL2CompiledMethod)
      unificationDB.reset
      _depth := 0
      if( m.body.isNotNil then: {
	m.body.check(fun(t :X_TL2Type, s :X_TL2Subst) {
	  setError(m.pos, "Method body does not conform to declared return type", {
	    t.checkSubType(m.methodType.range, s, X_TL2EmptySubst.instance)
	  })
	})
      })
    })
  }

_init :Void
  (* *)
{
    subTypeDB := X_TL2SubTypeDB.new
    varianceDB := Dictionary.new
    unificationDB := X_TL2UnificationDB.new
    
    logTypeChecks   := false
    logValueChecks  := false
    logSubTypeTests := false
    abortCheck      := false
    maxDepth        := 10000

    _currentClass    := nil
    dependencies     := Dictionary.new
    agenda           := Dictionary.new

    classManager.observers.add(self)
    pool.observers.add(self)

      ; insert all pre-bootstrap classes into the agenda 
    if( classManager.root.active then: {
      classManager.classTable.do(fun(c :X_TL2ClassPublic) {
        classChanged(classManager, c)
      })
    })
    
    self
  }

abort :Void
  (* *)
{
    _depth := _depth + 1
    if( _depth > maxDepth ||  abortCheck  then:
      { X_TL2TypeCheckAbort.new.raise })
  }

checkMeta(c :X_TL2ClassPublic) :Void
  (* *)
{
    ; check c''s Metaclass-Declaration
;       adds dependency to MetaClass
;    
    c.metaClassDeclaration.checkType(X_TL2EmptySubst.instance)

    ; ## check the Instance parameter of ConcreteClass 
  }

checkSelf(c :X_TL2ClassPublic) :Void
  (* *)
{
    c.selfTypeSig.type.checkType(X_TL2EmptySubst.instance)
  }

checkCPL(c :X_TL2ClassPublic) :Void
  (* *)
{
    ; depend on superclasses 
    classManager.superclasses(c).do(fun(c :X_TL2ClassPublic) { globalIdeReferenced(c.name) })
    
    cit ::= c.typeIde.type.visitor.
      ifClassInstanceType(fun(c :X_TL2ClassInstanceType) { c }).
      ifOperType(fun(o :X_TL2OperType) {
        o.range.visitor.
	  ifClassInstanceType(fun(c :X_TL2ClassInstanceType) { c }).
	visitType(o.range)
      }).
    visitType(c.typeIde.type)

    cit.checkCPL
  }

}
