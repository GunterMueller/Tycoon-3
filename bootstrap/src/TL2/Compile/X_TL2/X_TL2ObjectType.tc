class X_TL2ObjectType
super X_TL2TypeImpl
(*
 * This file is part of the Tycoon-2 system.
 *
 * The Tycoon-2 system is free software; you can redistribute it and/or
 * modify it under the terms of the GNU General Public License
 * as published by the Free Software Foundation (Version 2).
 *
 * The Tycoon-2 system is distributed in the hope that it will be useful,
 * but WITHOUT ANY WARRANTY; without even the implied warranty
 * of MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.
 * See the GNU General Public License for more details.
 * 
 * You should have received a copy of the GNU General Public
 * License along with the Tycoon-2 system; see the file LICENSE.
 * If not, write to AB 4.02, Softwaresysteme, TU Hamburg-Harburg
 * D-21071 Hamburg, Germany. (http://www.sts.tu-harburg.de)
 * 
 * Copyright (c) 1996-1998 Higher-Order GmbH, Hamburg. All rights reserved.
 *
*)
meta AbstractClass
{
accept(T <: Void, visitor :X_TL2TypeVisitor(T)) :T
  (* *)
{ visitor.visitObjectType(self) }

selfTypeSig :X_TL2TypeIde
  (* *)
deferred

checkSubObjectType(big :X_TL2ObjectType, subst :X_TL2Subst, bigSubst :X_TL2Subst) :Void
  (* *)
{
    ; to be able to keep big''s Self type, we must lookup on it, not on 'big'
;       Unfortunately, we lose the substitutions, if we use the original Self.
;       Therefore we build a new one pointing to a X_TL2SubstType-wrapped big.
;    
    bigSelf ::= big.selfTypeSig
    newBigSelf ::= X_TL2IdeType.new(X_TL2SubTypeIde.new(bigSelf.pos, "bigSelf", X_TL2SubstType.new(big, bigSubst)))
    
    mySelf  ::= self.selfTypeSig
    newMySelf ::= X_TL2IdeType.new(X_TL2SubTypeIde.new(mySelf.pos, "smallSelf", X_TL2SubstType.new(self, subst)))

    ; furthermore, after lookup, newBigSelf is to be equal to newMySelf 

    equalSelfs ::= X_TL2ConsSubst.new(newBigSelf.ide, newMySelf, X_TL2EmptySubst.instance)
    
    newBigSelf.methodsDo(X_TL2EmptySubst.instance, false, fun(mb :X_TL2Method, sb :X_TL2Subst) {
      ; equal both Selfs 
      sb ::= X_TL2CompositeSubst.new(sb, equalSelfs)

      newMySelf.lookup(mb.selector, X_TL2EmptySubst.instance, true, fun(ms :X_TL2Method, ss :X_TL2Subst) {
	if( ms.isPrivate then: {
	  raiseError
	})

	; when subtyping the methods, we must ensure that both Self types are the same.
;	   Therefore the subtitution is enlarged by [ newMySelf / newBigSelf ]
;	
	ms.methodType.checkSubType(mb.methodType, ss, sb)
      })
    })
  }

checkSubNilType(big :X_TL2NilType, subst :X_TL2Subst, bigSubst :X_TL2Subst) :Void
  (* *)
{ raiseSubTypeError(self, big) }

checkSubApplyType(big :X_TL2ApplyType,  subst :X_TL2Subst, bigSubst :X_TL2Subst) :Void
  (* *)
{
    big.expose(bigSubst, true, fun(big :X_TL2Type, bigSubst :X_TL2Subst) { checkSubType(big, subst, bigSubst) })
  }

checkSubOperType(big :X_TL2OperType, subst :X_TL2Subst, bigSubst :X_TL2Subst) :Void
  (* *)
{
    raiseSubTypeError(self, big)
  }

apply(T <: Void, args :List(X_TL2Type), subst :X_TL2Subst, argsSubst :X_TL2Subst, exact :Bool, cont :Fun(:X_TL2Type, :X_TL2Subst):T) :T
  (* *)
{
    if( args.isEmpty then: { cont[self, subst] } else: { raiseError }) 
  }

checkApplication(args :List(X_TL2Type), subst :X_TL2Subst) :Void
  (* *)
{
    if( args.isEmpty then: { } else: { raiseError }) 
  }

private

doExpose(T <: Void, subst :X_TL2Subst, exact :Bool, cont :Fun(:X_TL2Type, :X_TL2Subst):T) :T
  (* *)
{
    cont[self, subst]
  }

}
