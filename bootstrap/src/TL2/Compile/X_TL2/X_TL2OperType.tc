class X_TL2OperType
super X_TL2TypeImpl, X_TL2ApplyMixin
(*
 * This file is part of the Tycoon-2 system.
 *
 * The Tycoon-2 system is free software; you can redistribute it and/or
 * modify it under the terms of the GNU General Public License
 * as published by the Free Software Foundation (Version 2).
 *
 * The Tycoon-2 system is distributed in the hope that it will be useful,
 * but WITHOUT ANY WARRANTY; without even the implied warranty
 * of MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.
 * See the GNU General Public License for more details.
 * 
 * You should have received a copy of the GNU General Public
 * License along with the Tycoon-2 system; see the file LICENSE.
 * If not, write to AB 4.02, Softwaresysteme, TU Hamburg-Harburg
 * D-21071 Hamburg, Germany. (http://www.sts.tu-harburg.de)
 * 
 * Copyright (c) 1996-1998 Higher-Order GmbH, Hamburg. All rights reserved.
 *
*)
meta X_TL2OperTypeClass
{
domain :List(X_TL2TypeIde)

range :X_TL2Type

accept(T <: Void, visitor :X_TL2TypeVisitor(T)) :T
  (* *)
{ visitor.visitOperType(self) }

apply(T <: Void, args :List(X_TL2Type), subst :X_TL2Subst, argsSubst :X_TL2Subst, exact :Bool, cont :Fun(:X_TL2Type, :X_TL2Subst):T) :T
  (* applies self to args,
     and calls cont with the result
  *)
{
    ; domain.forAll(!inSubst) 
    applyArgs(pos, domain, args, subst, argsSubst, range, false, false, false, cont)
  }

checkApplication(args :List(X_TL2Type), subst :X_TL2Subst) :Void
  (* *)
{
    applyArgs(pos, domain, args, subst, subst, range, true, true, false, fun(:X_TL2Type, :X_TL2Subst) {})
  }

lookupWithSelf(T <: Void,
	         selector :Symbol,
	         subst :X_TL2Subst,
		 withPrivate :Bool,
		 selfType :X_TL2Type,
		 selfTypeSubst :X_TL2Subst,
	         cont: Fun(:X_TL2Method, :X_TL2Subst):T) :T
  (* *)
{
    raiseError
  }

methodsDoWithSelf(subst :X_TL2Subst,
		    withPrivate :Bool,
		    selfType :X_TL2Type,
		    selfTypeSubst :X_TL2Subst,
		    callback: Fun(:X_TL2Method, :X_TL2Subst):Void) :Void
  (* *)
{
    raiseError
  }

checkSubObjectType(big :X_TL2ObjectType, subst :X_TL2Subst, bigSubst :X_TL2Subst) :Void
  (* subtype tests: inherits
     checkSubIdeType
     checkSubUnifyType
     checkSubMethodType
  
 oper is no subtype of parameterless object types *)
{ raiseSubTypeError(self, big) }

checkSubNilType(big :X_TL2NilType, subst :X_TL2Subst, bigSubst :X_TL2Subst) :Void
  (* *)
{ raiseSubTypeError(self, big) }

checkSubApplyType(big :X_TL2ApplyType, subst :X_TL2Subst, bigSubst :X_TL2Subst) :Void
  (* *)
{
    big.expose(bigSubst, true, fun(big :X_TL2Type, bigSubst :X_TL2Subst) { checkSubType(big, subst, bigSubst) })
  }

checkSubOperType(big :X_TL2OperType, subst :X_TL2Subst, bigSubst :X_TL2Subst) :Void
  (* sub oper
     A = ForAll(B <: C).D
     E = ForAll(F <: G).H

     F <: G |- A(F) type   F <: G |- D[F / B] <: H
    -----------------------------------------------
                    |- A <: E

  *)
{
    bigArgs ::= List.fromReader(big.domain.map(fun(i :X_TL2TypeIde) {
      X_TL2IdeType.new(i)
    }))

    applyArgs(pos, domain, bigArgs, subst, bigSubst, range, true, false, true, fun(range :X_TL2Type, subst :X_TL2Subst) {
      range.checkSubType(big.range, subst, bigSubst)
    })
  }

prettyPrint(out :Printer) :Void
  (* *)
{
    out.writeAll("(\\-/")
    domain.doBetween(fun(i :X_TL2TypeIde) { i.prettyPrint(out) }, { out.writeAll(", ") })
    out.writeAll(".")
    range.prettyPrint(out)
    out.writeAll(")")
  }

private

doExpose(T <: Void, subst :X_TL2Subst, exact :Bool, cont :Fun(:X_TL2Type, :X_TL2Subst):T) :T
  (* *)
{
    cont[self, subst]
  }

}
