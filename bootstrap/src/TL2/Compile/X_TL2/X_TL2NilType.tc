class X_TL2NilType
super X_TL2TypeImpl, X_TL2LeafMixin
(*
 * This file is part of the Tycoon-2 system.
 *
 * The Tycoon-2 system is free software; you can redistribute it and/or
 * modify it under the terms of the GNU General Public License
 * as published by the Free Software Foundation (Version 2).
 *
 * The Tycoon-2 system is distributed in the hope that it will be useful,
 * but WITHOUT ANY WARRANTY; without even the implied warranty
 * of MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.
 * See the GNU General Public License for more details.
 * 
 * You should have received a copy of the GNU General Public
 * License along with the Tycoon-2 system; see the file LICENSE.
 * If not, write to AB 4.02, Softwaresysteme, TU Hamburg-Harburg
 * D-21071 Hamburg, Germany. (http://www.sts.tu-harburg.de)
 * 
 * Copyright (c) 1996-1998 Higher-Order GmbH, Hamburg. All rights reserved.
 *
 bottom element of type lattice *)
meta SingletonClass(X_TL2NilType)
{
accept(T <: Void, visitor :X_TL2TypeVisitor(T)) :T
  (* *)
{ visitor.visitNilType(self) }

reduce(T <: Void, s :X_TL2Subst, cont: Fun(:X_TL2Type, :X_TL2Subst):T) :T
  (* *)
{
    cont[self, X_TL2EmptySubst.instance]
  }

scope(env :X_TL2Scoper) :X_TL2NilType
  (* *)
{ self }

prettyPrint(out :Output) :Void
  (* *)
{
    out.writeAll("Nil")
  }

lookupWithSelf(T <: Void,
	 selector :Symbol,
	 subst :X_TL2Subst,
	 withPrivate :Bool,
	 selfType :X_TL2Type,
	 selfTypeSubst :X_TL2Subst,
	 cont :Fun(:X_TL2Method, :X_TL2Subst):T) :T
  (* typechecker *)
{
    raiseError
  }

checkSubNilType   (big :X_TL2NilType,    subst :X_TL2Subst, bigSubst :X_TL2Subst) :Void
  (* *)
{ ; success 
}

checkSubObjectType(big :X_TL2ObjectType, subst :X_TL2Subst, bigSubst :X_TL2Subst) :Void
  (* *)
{ ; success 
}

checkSubApplyType (big :X_TL2ApplyType,  subst :X_TL2Subst, bigSubst :X_TL2Subst) :Void
  (* X_TL2Type applications are never opers *)
{ ; success 
}

checkSubIdeType   (big :X_TL2IdeType,    subst :X_TL2Subst, bigSubst :X_TL2Subst) :Void
  (* *)
{
    ; Nil <: T gdw. T <: Void 
    
    big.checkSubType(X_TL2VoidType.instance, subst, X_TL2EmptySubst.instance)
  }

checkSubOperType  (big :X_TL2OperType,   subst :X_TL2Subst, bigSubst :X_TL2Subst) :Void
  (* *)
{
    raiseSubTypeError(self, big)
  }

checkSubSubstType (big :X_TL2SubstType,  subst :X_TL2Subst, bigSubst :X_TL2Subst) :Void
  (* *)
{
    checkSubType(big.type, subst, X_TL2CompositeSubst.new(big.substi, bigSubst))
  }

checkApplication(args :List(X_TL2Type), subst :X_TL2Subst) :Void
  (* *)
{
    raiseError
  }

private

doExpose(T <: Void, subst :X_TL2Subst, exact :Bool, cont :Fun(:X_TL2Type, :X_TL2Subst):T) :T
  (* *)
{
    cont[self, subst]
  }

}
