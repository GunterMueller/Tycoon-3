class TL2Slot
super TL2ProgramObject
(*
 * This file is part of the Tycoon-2 system.
 *
 * The Tycoon-2 system is free software; you can redistribute it and/or
 * modify it under the terms of the GNU General Public License
 * as published by the Free Software Foundation (Version 2).
 *
 * The Tycoon-2 system is distributed in the hope that it will be useful,
 * but WITHOUT ANY WARRANTY; without even the implied warranty
 * of MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.
 * See the GNU General Public License for more details.
 * 
 * You should have received a copy of the GNU General Public
 * License along with the Tycoon-2 system; see the file LICENSE.
 * If not, write to AB 4.02, Softwaresysteme, TU Hamburg-Harburg
 * D-21071 Hamburg, Germany. (http://www.sts.tu-harburg.de)
 * 
 * Copyright (c) 1996-1998 Higher-Order GmbH, Hamburg. All rights reserved.
 *
*)
meta TL2SlotClass
{
name :Symbol

type :TL2Type

documentation :String

isPrivate :Bool

isComponent :Bool

prettyPrint(out:Printer) :Void
  (* *)
{ out ; << (isPrivate?{"private "}:{"public "}) 
      << name <<if(isComponent then:{" @:"}else:{" :"})<<type
  if( documentation.isNotNil
  then: { out.withRelIndent(2, {
        out << "\n(*"<<documentation<<"*)"
      }) })
  out.nl
}

scopeSlot(env :TL2Scoper) :Void
  (* *)
{ type := type.scope(env) }

selectors :Array(TL2Selector)
{
  if( isComponent then: {
    #( TL2Selector.new(name, #(), #()),
       TL2Selector.new(Symbol.new(name+"@"), #(), #()),
       TL2Selector.new(Symbol.new(name+":="), #(true), #()))
  } else: {
    #( TL2Selector.new(name, #(), #()),
       TL2Selector.new(Symbol.new(name+":="), #(false), #()))
  })
}

accessMethods(offset :Int, isStruct :Bool) :Array(TL2SlotMethod)
  (* find or create a set of slot method for this slot with the desired properties *)
{
  found ::=
    cache.detect(fun(a :Array(TL2SlotMethod)) {
      m ::= a[0] ;; the same for all elements of a
      m.offset = offset && m.isStruct = isStruct
    })
  if( found.isNil then: {
    found := generate(offset, isStruct)
    cache := List.cons(found, cache)
  })
  found
}

private

generate(offset :Int, isStruct :Bool) :Array(TL2SlotMethod)
{
  if( isStruct then: {
    if( isComponent then: {
      Array(:TL2SlotMethod).with3(
        TL2CSlotReferenceMethod.new(self, offset),
	TL2CSlotTakeFromMethod.new(self, offset),
	TL2CSlotMoveToMethod.new(self, offset) )
    } else: {
      Array(:TL2SlotMethod).with2(    
        TL2CSlotUpdateMethod.new(self, offset),
        TL2CSlotAccessMethod.new(self, offset) )
    })
  } else : {
    if( isComponent then: {
      Array(:TL2SlotMethod).with3(
        TL2SlotReferenceMethod.new(self, offset),
	TL2SlotTakeFromMethod.new(self, offset),
	TL2SlotMoveToMethod.new(self, offset) )
    } else: {
      Array(:TL2SlotMethod).with2(    
        TL2SlotUpdateMethod.new(self, offset),
        TL2SlotAccessMethod.new(self, offset) )
    })
  })
}

cache :List(Array(TL2SlotMethod))
  (* all slot methods generated for this slot so far *)

_init :Void
{ super._init
  cache := emptyList
}
}
