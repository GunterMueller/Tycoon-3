class BEArgumentVar
super BEVariable
(*
 * This file is part of the Tycoon-2 system.
 *
 * The Tycoon-2 system is free software; you can redistribute it and/or
 * modify it under the terms of the GNU General Public License
 * as published by the Free Software Foundation (Version 2).
 *
 * The Tycoon-2 system is distributed in the hope that it will be useful,
 * but WITHOUT ANY WARRANTY; without even the implied warranty
 * of MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.
 * See the GNU General Public License for more details.
 * 
 * You should have received a copy of the GNU General Public
 * License along with the Tycoon-2 system; see the file LICENSE.
 * If not, write to AB 4.02, Softwaresysteme, TU Hamburg-Harburg
 * D-21071 Hamburg, Germany. (http://www.sts.tu-harburg.de)
 * 
 * Copyright (c) 1996-1998 Higher-Order GmbH, Hamburg. All rights reserved.
 *
*)
meta BEArgumentVarClass
{
idx :Int

genLoad(writer :BEByteCodeWriter) :Void
  (* *)
{
  assert({ !ide.component })
  if( ide.celled then: {
    genLoadRef(writer)
    writer.cellLoad
  } else: {
    writer.loadArgument(idx)
  })
}

genStore(writer :BEByteCodeWriter) :Void
  (* *)
{
  assert({ !ide.component })
  if( ide.accessedFromClosure then: {
    genLoadRef(writer)
    writer.cellStore
  } else: {
    writer.storeArgument(idx)
  })
}

genLoadRef(writer :BEByteCodeWriter) :Void
  (* *)
{ writer.loadArgument(idx) }

genReference(writer :BEByteCodeWriter) :Void
  (* *)
{
  assert({ ide.component })
  if( ide.accessedFromClosure then: {
    genLoadRef(writer)
    writer.cellReference
  } else: {
    writer.referenceArgument(idx)
  })
}

genTakeFrom(writer :BEByteCodeWriter) :Void
  (* *)
{
  assert({ ide.component })
  if( ide.accessedFromClosure then: {
    genLoadRef(writer)
    writer.takeFromCell
  } else: {
    writer.takeFromArgument(idx)
  })
}

genMoveTo(writer :BEByteCodeWriter) :Void
  (* *)
{
  assert({ ide.component })
  if( ide.accessedFromClosure then: {
    genLoadRef(writer)
    writer.moveToCell
  } else: {
    writer.moveToArgument(idx)
  })
}

genCell(writer :BEByteCodeWriter) :Void
{
  assert({ ide.celled })
  if( ide.component then: {
    writer.takeFromArgument(idx)
    writer.componentCellNew
    writer.storeArgument(idx)
    writer.pop
  } else: {
    writer.loadArgument(idx)
    writer.cellNew
    writer.storeArgument(idx)
    writer.pop
  })
}
}
