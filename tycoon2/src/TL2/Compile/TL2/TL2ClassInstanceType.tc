class TL2ClassInstanceType
super TL2ObjectType
(*
 * This file is part of the Tycoon-2 system.
 *
 * The Tycoon-2 system is free software; you can redistribute it and/or
 * modify it under the terms of the GNU General Public License
 * as published by the Free Software Foundation (Version 2).
 *
 * The Tycoon-2 system is distributed in the hope that it will be useful,
 * but WITHOUT ANY WARRANTY; without even the implied warranty
 * of MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.
 * See the GNU General Public License for more details.
 * 
 * You should have received a copy of the GNU General Public
 * License along with the Tycoon-2 system; see the file LICENSE.
 * If not, write to AB 4.02, Softwaresysteme, TU Hamburg-Harburg
 * D-21071 Hamburg, Germany. (http://www.sts.tu-harburg.de)
 * 
 * Copyright (c) 1996-1998 Higher-Order GmbH, Hamburg. All rights reserved.
 *
*)
meta TL2ClassInstanceTypeClass
{
class_ :TL2ClassPublic

accept(T <: Void, visitor :TL2TypeVisitor(T)) :T
  (* *)
{ visitor.visitClassInstanceType(self) }

selfTypeSig :TL2TypeIde
  (* *)
{ class_.selfTypeSig }

cpl :List(TL2CPLType)
  (* ## cache *)
{
    super._init
    
    d ::= Dictionary(:String, :TL2CPLType).new

    class_.supers.do(fun(t :TL2CPLType) {
      subst ::= t.oper.apply(t.args, TL2EmptySubst.instance, TL2EmptySubst.instance, true, fun(:TL2Type, s :TL2Subst) { s })
      oper  ::= class_.classManager.get(t.className).typeIde.type

      oper.visitor
        .ifClassInstanceType(fun(oper :TL2ClassInstanceType) {
	  oper.cpl.do(fun(t :TL2CPLType) { addCPLType(d, t) })
	})
	.ifOperType(fun(oper :TL2OperType) {
	  _typeCast(oper.range, :TL2ClassInstanceType).cpl.do(
	    fun(t :TL2CPLType) {
	      tS ::= TL2CPLType.new(t.ide, List.fromReader(t.args.map(fun(arg :TL2Type) { substituteType(arg, subst) })))
	      addCPLType(d, tS)
	   })
	 })
	.visitType(oper)
    })

    addCPLType(d,
      TL2CPLType.new(
        class_.typeIde,
	List.fromReader(class_.domain.map(fun(t :TL2TypeIde) { TL2IdeType.new(t) }))
      )
    )

  List.append(
    List.with1(d[class_.name]),
    class_.superclasses.map(fun(c :TL2ClassPublic) {
      d[c.name]
    }).as(List))
}

prettyPrint(out :Printer) :Void
  (* *)
{
    out.writeAll(class_.name)
  }

lookupWithSelf(T <: Void,
	 selector :Symbol,
	 subst :TL2Subst,
	 withPrivate :Bool,
	 selfType :TL2Type,
	 selfTypeSubst :TL2Subst,
	 cont :Fun(:TL2Method, :TL2Subst):T) :T
  (* *)
{
;    tycoon.stdout.writeln("lookup "+class_.name+"::"+selector), 
    method :TL2Method := nil
    
    md ::= class_.methodDictionaries
    while({ method.isNil &&  !md.isEmpty } do: {
      dict ::= md.head
      if( dict.includesKey(selector) then: {
        method := dict[selector]

	; notify typeChecker 
	if( dict == tycoon.tl.pool.poolMethods then: {
	  tycoon.tl.typeChecker.globalIdeReferenced(selector)
	})
	
	nil
      } else: {
        md := md.tail
	nil
      })
    })

    if( method.isNil then: {
      raiseError
    })

    if( method.isPrivate &&  !withPrivate  then: {
      raiseError
    })

    cont[method, TL2CompositeSubst.new(
		   TL2CompositeSubst.new(cplSubst, subst),
		   TL2ConsSubst.new(class_.selfTypeSig,
				 TL2SubstType.new(selfType, selfTypeSubst),
				 TL2EmptySubst.instance
				 )
		 )]
  }

methodsWithSelfDo(subst :TL2Subst,
		    withPrivate :Bool,
		    selfType :TL2Type,
		    selfTypeSubst :TL2Subst,
	            callback :Fun(:TL2Method, :TL2Subst):Void) :Void
  (* *)
{
    subst ::= TL2CompositeSubst.new(
		   TL2CompositeSubst.new(cplSubst, subst),
		   TL2ConsSubst.new(class_.selfTypeSig,
				 TL2SubstType.new(selfType, selfTypeSubst),
				 TL2EmptySubst.instance
				 )
		 )

    seen ::= Set(:Symbol).new

    class_.methodDictionaries.do(fun(dict :KeyedContainer(Symbol,TL2Method)) {
      if( dict !== tycoon.tl.pool.poolMethods then: {
	dict.keysAndElementsDo(fun(selector :Symbol, method :TL2Method) {
	  if( !method.isPrivate ||  withPrivate  then: {
	    if( !seen.includes(selector) then: {
	      seen.add(selector)

	      callback[method, subst]
	    })
	  })
	})
      })
    })
  }

resetCPL :Void
  (* force recompute of cplSubst *)
{ _cplSubst := nil }

cplSubst :TL2Subst
  (* *)
{
    ; this must be done lazily, because expose must evaluate to the receiver
;       which must happen after class.typeIde.type := TL2ClassInstanceType(class)
;    
    if( _cplSubst.isNil then: {
      _cplSubst := cpl.inject(TL2EmptySubst.instance,
		  fun(subst :TL2Subst, ct :TL2CPLType) {
		    ct.expose(subst, false, fun(exposed :TL2Type, es :TL2Subst) {
		      exposed.visitor.
			ifClassInstanceType(fun(exposed :TL2ClassInstanceType) {
			  TL2CompositeSubst.new(es, TL2ConsSubst.new(
			    exposed.class_.selfTypeSig,
			    TL2IdeType.new(class_.selfTypeSig),
			    subst)
			  )
			}).
		      visitType(exposed)
		    })
		  })
   })

   _cplSubst
 }

checkSubClassInstanceType(big :TL2ClassInstanceType, subst :TL2Subst, bigSubst :TL2Subst) :Void
  (* *)
{
   if( subst == TL2EmptySubst.instance &&  bigSubst == TL2EmptySubst.instance  &&  class_.isSubClassOf(big.class_)  then:
   { ; Subclassing implies Subtyping 
}
   else:
   { super.checkSubClassInstanceType(big, subst, bigSubst) })
 }

checkCPL :Void
  (* called by TL2TypeChecker, check the validity of the TL2CPL *)
{
   ; may indicate that superclasses have changed --- and their Self 
   _cplSubst := nil
   
   ; maps from TL2Method selector to TL2Method last found
;   
   seen ::= Dictionary(:Symbol, :TL2Method).new

   class_.methodDictionaries.reverseReader.do(fun(d :KeyedContainer(Symbol, TL2Method)) {
     d.do(fun(m :TL2Method) {
       if( seen.includesKey(m.selector) then: {   ; TL2Method overridden 
         over ::= seen[m.selector]

	 setError2(m.pos, over.pos, "Public method overridden by private: "+m.selector, {
	   if( m.isPrivate && !over.isPrivate then: {
	     raiseError
	   })
	 })
	 
	 setError2(m.pos, over.pos, "Overriding method not subtype: "+m.selector, {
	   m.methodType.checkSubType(over.methodType, cplSubst, cplSubst)
	 })
       })

       seen[m.selector] := m
     })
   })
 }

private

substituteType(t :TL2Type, s :TL2Subst) :TL2Type
  (* chance again to drop some ide''s --- use TL2Type::reduce *)
{
  t.reduce(s, fun(t :TL2Type, s :TL2Subst) {
    if( s.isEmpty then: {
      t
    } else: {
      TL2SubstType.new(t, s)
    })
  })
}

addCPLType(d :Dictionary(String, TL2CPLType), t :TL2CPLType) :Void
  (*
    It is prohibited to use the same class parameterized with different types
    in the transitive closure of superclasses.
    ## 'Different types' is interpreted by name not by structure
    ##
    ## class X super Y(Z);
    ##
    ## class A super T(X), T(Y(Z)); is therefore also forbidden.
    ##
  *)
{
  ;; ## check not implemented
  ;; ## if exists, should compare d[t.ide.name] with t
  d[t.ide.name] := t
}

_cplSubst :TL2Subst
  (* holds the subst for the whole TL2CPL *)

_init1(c :TL2ClassPublic) :Void
  (* *)
{
    class_ := c
  }

}
