class TL2SendValue
super TL2ApplyMixin, TL2Value
(*
 * This file is part of the Tycoon-2 system.
 *
 * The Tycoon-2 system is free software; you can redistribute it and/or
 * modify it under the terms of the GNU General Public License
 * as published by the Free Software Foundation (Version 2).
 *
 * The Tycoon-2 system is distributed in the hope that it will be useful,
 * but WITHOUT ANY WARRANTY; without even the implied warranty
 * of MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.
 * See the GNU General Public License for more details.
 * 
 * You should have received a copy of the GNU General Public
 * License along with the Tycoon-2 system; see the file LICENSE.
 * If not, write to AB 4.02, Softwaresysteme, TU Hamburg-Harburg
 * D-21071 Hamburg, Germany. (http://www.sts.tu-harburg.de)
 * 
 * Copyright (c) 1996-1998 Higher-Order GmbH, Hamburg. All rights reserved.
 *
*)
meta TL2SendValueClass
{
receiver :TL2Value

  (* modified by scoper *)

selector :Symbol

args :List(TL2Argument)

_keywords :Array(Symbol)
  (* for the last arguments *)

generator :TL2CodeGenerator
  (* set during "optimization", zapped during code generation 
 a0 a1 a2 a3
         k0 k1 *)

keywords :Array(Symbol)
  (* ### temp hack *)
{ k ::= _keywords
  if( k.isNotNil then: { k } else: { Array.with0 })
}

"keywords:="(k :Array(Symbol)) :Array(Symbol)
  (* *)
{ _keywords := k }

accept(T <: Void, v :TL2ValueVisitor(T)) :T
  (* *)
{ v.visitSend(self) }

isComponent :Bool
{
  selector.size > 0 && selector[selector.size-1] = '@'
}

isSend :Bool
  (* *)
{ true }

asSend :TL2SendValue
  (* *)
{ self }

plainArgs :Reader(TL2Argument)
  (* plain, i.e. non-keyword arguments *)
{
  args.subSequenceReader(0,args.size - keywords.size)
}

keywordArgs :Reader(Pair(Symbol, TL2Value))
  (* *)
{
  args.nthTail(args.size - keywords.size)
      .keysAndElementsMap(fun(i:Int, arg :TL2Argument) {
         sym ::= keywords[i]
	 arg.argumentCase(fun(arg :TL2Value) {
	   Pair.new(sym, arg)
	 }, fun(arg:TL2Type) {
	   assert({ arg.isValue }) ; false 
	 })
       })
}

hasPlain :Bool
  (* *)
{ args.size - keywords.size > 0
}

hasKeywords :Bool
  (* *)
{ !keywords.isEmpty }

prettyPrint(out :Printer) :Void
  (* *)
{
  printReceiver(out)
  selector.printOn(out)
  if( !args.isEmpty then: {
    ; all arguments up to the first non-leaf arg are
;       printed on the same line.  
    out.write('(')
    out.withRelIndent(2, {
      complex ::= false
      plainArgs.doBetween(fun(arg :TL2Argument) {
        if( arg.isType then: { out.write(':') })
	arg.prettyPrint(out)
	complex := complex ||  !arg.isLeaf 
      }, {
        out.writeAll(if( complex then: {",\n"} else: {", "}))
      })
      if( hasKeywords then: {
        if( hasPlain then: { out.nl })
	keywordArgs.doBetween(fun(p :Pair(Symbol, TL2Value)) {
	  out << p.first << ": "
	  p.second.prettyPrint(out)
	}, {
	  out.nl
	})
      })
      out.write(')')
    })
  })
}

printReceiver(out :Printer) :Void
  (* *)
{
  if( receiver.isLeaf | receiver.isSend  then: {
    receiver.prettyPrint(out)
  } else: {
    ; for cases like "(x := bla).isNil" 
    out.write('(')
    receiver.prettyPrint(out)
    out.write(')')
  })
  out.write('.')
}

children: Reader(TL2ProgramObject)
  (* *)
{
  List(:TL2ProgramObject).cons(receiver, args).reader
}

scope(env :TL2Scoper) :TL2SendValue
  (* *)
{
  receiver := receiver.scope(env)
  if( receiver.isComponent then: {
    env.errorLog.error(pos, "receiver in message send must not be a component")
  })
  args := List.fromReader(
    args.map(fun(a :TL2Argument) { a.scope(env) }))
  self
}

genCode(backEnd :BEMethodManager) :Void
  (* *)
{ generator.genCode(backEnd)
  generator := nil
}

genCodeIgnore(backEnd :BEMethodManager) :Void
  (* *)
{ generator.genCodeIgnore(backEnd)
  generator := nil
}

genCodeReturn(backEnd :BEMethodManager) :Void
  (* *)
{ generator.genCodeReturn(backEnd)
  generator := nil
}

genCodeBranch( backEnd :BEMethodManager,dest :BEMMLabel, branchIf :Bool) :Void
  (* *)
{ generator.genCodeBranch(backEnd, dest, branchIf)
  generator := nil
}

isSelfSend :Bool
  (* *)
{
  receiver.isIde &&  receiver.asIde.ide.name = "self" 
}

private

doCheck(T <: Void, cont :Fun(:TL2Type, :TL2Subst):T) :T
  (* *)
{
    receiverType :TL2Type := nil
    receiverSubst :TL2Subst := nil
    
    receiver.check(fun(rt :TL2Type, rs :TL2Subst) { receiverType := rt receiverSubst := rs })

    setError(pos, "Method not found: \""+selector+"\"", {
      receiverType.lookup(selector, receiverSubst, true,
	fun(m :TL2Method, subst :TL2Subst) {
	  if( m.isPrivate then: {
	    if( isSelfSend ||  isMetaClassSend(receiverType)  then: {} else: {
	      setError(pos, "Private method access: \""+selector+"\"", {
		raiseError
	      })
	    })
	  })
	  if( !keywords.sequenceEqual(m.methodType.keywords) then: {
	    tycoon.stdout << keywords << '\n'
			  << m.methodType.keywords << '\n'
	    ; ### keyword args 
	    setError2(pos, m.pos, "keyword lists not implemented", { raiseError })
	  })
	  applyArgs(pos, m.methodType.domain, args, subst, subst, m.methodType.range, true, true, false, cont)
	}
      )
    })
  }

isMetaClassSend(receiverType :TL2Type) :Bool
  (* Hopefully, the Instance ide of ConcreteClass is preserved over the 'let instance = _new'
     until this private send *)
{
    receiverIde :TL2TypeIde := nil
    
    typeChecker.currentClass.isNotNil &&  typeChecker.currentClass."class" == MetaClass  && (
      receiverType.visitor.
        ifSubstType(fun(receiverType :TL2SubstType) {
	  receiverType.type.visitor.
	    ifIdeType(fun(receiverType :TL2IdeType) {
	      receiverIde := receiverType.ide
	    }).
	    otherwise(fun(:TL2Type) { nil }).
	  visitType(receiverType.type)
	}).
	ifIdeType(fun(receiverType :TL2IdeType) {
	  receiverIde := receiverType.ide
	}).
	otherwise(fun(:TL2Type) { nil }).
      visitType(receiverType)

      ; Either receiverIde is nil or it contains the TL2TypeIde of the type of the receiver 

      receiverIde == ConcreteClass.domain[0] ||
      receiverIde == CStructClass.domain[0] ||
      receiverIde == WeakRefClass.domain[1] ||
      receiverIde == ThreadClass.domain[1]
    )
  }

}
