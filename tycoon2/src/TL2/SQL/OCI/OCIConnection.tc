(*
 * This file is part of the Tycoon-2 system.
 *
 * The Tycoon-2 system is free software; you can redistribute it and/or
 * modify it under the terms of the GNU General Public License
 * as published by the Free Software Foundation (Version 2).
 *
 * The Tycoon-2 system is distributed in the hope that it will be useful,
 * but WITHOUT ANY WARRANTY; without even the implied warranty
 * of MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.
 * See the GNU General Public License for more details.
 * 
 * You should have received a copy of the GNU General Public
 * License along with the Tycoon-2 system; see the file LICENSE.
 * If not, write to AB 4.02, Softwaresysteme, TU Hamburg-Harburg
 * D-21071 Hamburg, Germany. (http://www.sts.tu-harburg.de)
 * 
 * Copyright (c) 1996-1998 Higher-Order GmbH, Hamburg. All rights reserved.
 *

Handle connections with data-sources via Oracle-Call-Interface

Author:  Michael Skusa

Date:    03-Sep-1996

Updates: (Date)		(Name)	(Description)
	 14-mar-1997	MS	replaced integers for return-, type- and
				error-codes with named constants
*)

class OCIConnection

super SQLConnection

metaclass SimpleConcreteClass(OCIConnection)

public methods

newStatement:OCIStatement
{
  !isClosed
  ? {
      let s=OCIStatement.new,
      s.open(_hdbc,self),
      s
    }
  : {nil}
}

setAutoCommit(on:Bool):Void
  (* on = true  enables AutoCommit, each single sql-statement is executed
                and commited immediately,
     on = false changes become persistent only if commit is called after
                their execution,

     raises OCIError.
   *)
{
  let er = on ? {oci.ocon(_hdbc)} 
              : {oci.ocof(_hdbc)},
  er != 0 ? {OCIError.new(_hdbc,_hdbc).raise}
}

commit():Void
{
  oci.ocom(_hdbc) != 0 ? {OCIError.new(_hdbc,_hdbc).raise}
}

rollback():Void
{
  oci.orol(_hdbc) != 0 ? {OCIError.new(_hdbc,_hdbc).raise}
}

private methods

_connect(database:String,user:String,password:String):Void
{
   _henv := tycoon.ansiC.malloc(256),
   _hdbc := tycoon.ansiC.malloc(64),
   let er = oci.olog(_hdbc,_henv,user,oci.NTS,password,oci.NTS,
                     database,oci.NTS),
   er != 0 ? {let error = OCIError.new(_hdbc,_hdbc),
              tycoon.ansiC.free(_hdbc),_hdbc:=nil,
              tycoon.ansiC.free(_henv),_henv:=nil,
	      error.raise},
   _setDateFormat
}

_open(url:URL):Void
{
  let var database = "",
  let var user = "",
  let var password = "",
  url.arcs.size > 1 ? { database := url.arcs[1] }, 
  url.parameters.includesKey("user")
  ? { user := url.parameters["user"].head },
  url.parameters.includesKey("password") 
  ? { password := url.parameters["password"].head },
  _connect(database,user,password)
}

_releaseHandles():Void
{
  oci.ologof(_hdbc),
  tycoon.ansiC.free(_hdbc),
  tycoon.ansiC.free(_henv)
}

_setDateFormat:Void
{
  let statement = newStatement,
  statement.executeDirectUpdate("alter session set nls_date_format = \'YYYY-MM-DD HH24:MI:SS\'"),
  statement.close 
}

;



